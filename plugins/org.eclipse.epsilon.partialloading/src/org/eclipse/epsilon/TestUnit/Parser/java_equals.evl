model javaMM driver EMF {
nsuri = "http://www.eclipse.org/MoDisco/Java/0.2.incubation/java" 
};

@cached
operation AbstractTypeDeclaration getPublicMethods() : Collection<MethodDeclaration> {
    return self.bodyDeclarations.select(bd : MethodDeclaration | 
        bd.modifier.isDefined() and bd.modifier.visibility == VisibilityKind#public);
}

$parallel self.getPublicMethods().size() > 5

context ClassDeclaration {

	constraint hasEquals {
		guard : self.satisfies("hasHashCode")
		check : self.getPublicMethods().exists(method |
			method.name == "equals" and
			method.parameters.size() == 1 and
			method.parameters.first().type.type.name == "Object" and
			method.returnType.type.isTypeOf(PrimitiveTypeBoolean)
		)
	}
	
	@lazy
	constraint hasHashCode {
		check : self.getPublicMethods().exists(method |
			method.name == "hashCode" and
			method.parameters.isEmpty() and
			method.returnType.type.isTypeOf(PrimitiveTypeInt)
		)
	}
}